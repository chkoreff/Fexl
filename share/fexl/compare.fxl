# Comparison functions capable of handling long, double, and string.

# LATER 20120826 instead of -9, how about calling type_error directly?
\cmp =
	(\x\y
	is_long x
		(
		is_long y (long_cmp x y);
		is_double y (double_cmp (long_double x) y);
		-9
		);
	is_double x
		(
		is_double y (double_cmp x y);
		is_long y (double_cmp x (long_double y));
		-9
		);
	is_string x
		(
		is_string y (string_cmp x y);
		-9
		);
	-9
	)

\compare = (\x\y order (cmp x y))

\gt = (\x\y order (cmp x y) F F T)
\eq = (\x\y order (cmp x y) F T F)
\ge = (\x\y order (cmp x y) F T T)

\lt = (\x\y order (cmp x y) T F F)
\ne = (\x\y order (cmp x y) T F T)
\le = (\x\y order (cmp x y) T T F)

\min = (\x\y order (cmp x y) x x y)
\max = (\x\y order (cmp x y) y y x)

\ >   = gt
\ =   = eq
\ ==  = eq
\ >=  = ge

\ <   = lt
\ !=  = ne
\ <=  = le

define "cmp" cmp;
define "compare" compare;

define "gt" gt;
define "eq" eq;
define "ge" ge;

define "lt" lt;
define "ne" ne;
define "le" le;

define "min" min;
define "max" max;

define ">" >;
define "=" =;
define "==" ==;
define ">=" >=;

define "<" <;
define "!=" !=;
define "<=" <=;
void
