# Testing with a version that prints "get " when it reads a char.
\fgetc=
	(\fh
	put "get "
	fgetc fh
	)

\fh_entropy==
	(\=
	\source="/dev/urandom" # Normally you'd use this.
	\source="/dev/zero" # Tests all zeroes.

	# Testing with fixed file contents.
	\source=(path_under dir_base "src/test/chars.fxl")

	\fh=(fopen source "r")
	is_void fh (error ["Could not open "source]);
	fh
	)

\list_entropy==(stream_values (fgetc fh_entropy))

# Get the next n bytes from the entropy source.  The "\==" is so it doesn't
# read the first byte immediately.  I only care about that because I want the
# first "get" to happen AFTER I print the opening bracket.  The "\=" is so I
# return a *fixed* list of the next n bytes, so it shows me the same thing when
# I print the list multiple times, instead of reading new entropy bytes every
# time I print it.
\get_bytes=(\n \== \= first n; list_entropy)

\get_random_id==
	(
	\bytes=(get_bytes 16)
	\bytes=(map (\ch hex_byte (ord ch)) bytes)
	to_str bytes
	)

\show=
	(\xs
	put "[ "
	each xs (\x put [(hex_byte;ord x) " "])
	say "]"
	)

\show_bytes==
	(
	\bytes=(get_bytes 16)
	show bytes
	show bytes
	show bytes
	nl
	)

\show_id==
	(
	\x=get_random_id
	say x
	say x
	say x
	nl
	)

show_bytes
show_bytes
show_id
show_id
