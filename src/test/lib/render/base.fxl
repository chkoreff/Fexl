# Indented output

\v_put=var_new
\v_nl=var_new
\v_say=var_new
\v_tab=var_new

\put==(var_get v_put)
\nl==(var_get v_nl ())
\say==(var_get v_say)

# Indent content with a new tab stop.
\indent==
	(\content
	\tab=(var_get v_tab)
	var_put v_tab (. tab TAB)
	content
	var_put v_tab tab
	)

# Slide over to the current tab stop.
\tab==(put (var_get v_tab))

# Redirect output functions to a file handle.
\set_output_fh==
	(\fh
	var_put v_tab ""
	var_put v_put (fput fh)
	var_put v_nl (yield; fnl fh)
	var_put v_say
		(\x
		fput fh (var_get v_tab)
		fsay fh x
		)
	)

# Run content with output functions sent to a file handle.
\use_output_fh==
	(\fh\content

	\save_tab=(var_get v_tab)
	\save_put=(var_get v_put)
	\save_nl=(var_get v_nl)
	\save_say=(var_get v_say)

	set_output_fh fh
	content

	var_put v_tab save_tab
	var_put v_put save_put
	var_put v_nl save_nl
	var_put v_say save_say
	)

set_output_fh stdout

\form
def "put" put;
def "nl" nl;
def "say" say;
def "indent" indent;
def "tab" tab;
def "use_output_fh" use_output_fh;
form
