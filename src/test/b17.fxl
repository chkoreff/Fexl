#use_syntax_2
value; std; (value; std; use "test.fxl") \;

\show_result=
	(
	\show=
		(\label\text
		say label
		say "["
		put text
		say "]"
		)

	\out\err\status
	show "out:" out
	show "err:" err
	# Showing status more portably here.  Sometimes error codes on abnormal
	# termination can vary between systems.
	say ["status: "(eq status 0 "good" "fail")]
	nl
	)

\\test_run_function=
(
run_test "test_run_function";

\try=
	(\\child_fn\input
	run_function child_fn input show_result
	)

try
	(
	say "hello"
	trace "complain 1"
	\x=(value; std; parse stdin "")
	say ["x = "(as_str x)]

	trace "complain 2"
	say "bye"
	)
	"123"

try
	(
	)
	""

try
	(
	say "hello"
	error "oops"
	)
	""

# Test running out of memory.
try
	(
	limit_memory 4000000
	limit_stack  1000000

	# Make a nasty function that consumes an unbounded amount of memory.
	\\inf=
		(
		\S=(\\x\\y\\z x z; y z)  # Define the "fusion" operator.
		@ S S S  # Apply fixpoint to the fusion operator in a weird way.
		)

	# Invoke that function.
	inf
	)
	""

if F
(
# Test running out of time.
# This test is disabled to avoid slowing the test suite.
try
	(
	limit_time 1
	@ I
	)
	""
)
)

\\test_run_program=
(
run_test "test_run_program";

\try=
	(\code
	run_program [(argv 0)] code show_result
	)

try
~
#use_syntax_2
say "hello"
\x=(value; std; parse stdin "")
say ["x = "(as_str x)]
trace "complain"
error "oops"
\#
\a=(+ 246 3)
\b=47
(* a b)
~

try
~
say "BEG"
trace "complain"
say "END"
~
)

\\test_big_output=
(
run_test "test_big_output";
\out=(grab_output; each (range 1 200000) (\_ put "x"))
say ["grabbed "(length out)" bytes"]
)

test_run_function
test_run_program
test_big_output
