# Test standard context.

\test_context==
(\context

\try=
	(\name
	\f=(context name)
	say ["name "name" : "(is_defined f)]
	)

try "say"
try "is_defined"
try "standard"
try "+"
try "@"
try "def"
try "if"
try "each"
try "div"
try "show_list"
try "use_file"
try "lib"
try "use"
try "void"
try "dirname"
try "hex_digit"
try "read_ssv_file"
try "read_csv_file"
try "2.5"
try "0"
try "-3.56"
try "-3.56e-4"
try "-3.56e-4x"
try "x"
)

say "== test_full_context"
test_context
	(
	: standard;
	: (lib "date.fxl");
	: (lib "read_ssv.fxl");
	: (lib "read_csv.fxl");
	: (lib "html.fxl");
	: (use "test.fxl");
	void
	)

say "== test_restricted_context"
test_context
	(
	def "say" say;
	def "is_defined" is_defined;
	void
	)

(
say "== test_dynamic_context"

# This context defines 5 available functions.
\context=
	(
	def "say" say;
	def "seed_rand" seed_rand;
	def "rand" rand;
	def "fred" (say "I am Fred.");
	def "wilma" (say "I am Wilma.");
	void
	)

# The get function looks up a key in the context and dies if not defined.
\get=
	(\key
	\val=(context key)
	is_void val (error ["Undefined symbol "key]);
	val
	)

# Now I evaluate some code in a context which defines only the "get" function.
# The code must explicitly get every single function it wants to use.
evaluate
(
def "get" get;
void
) \;

get "say" \say
get "seed_rand" \seed_rand
get "rand" \rand
#get "barney" \barney # This would be an error.
get "fred" \fred
get "wilma" \wilma

seed_rand 0.1964
fred
say rand
say rand
wilma
)
