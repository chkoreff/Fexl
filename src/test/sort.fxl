# Here we use a highly restricted context which provides only the few functions
# we need to test sorting.  I don't have to do it this way, but I'm exercising
# the context mechanism.
\=
(
\append==(\xs\ys xs ys \x\xs [x;append xs ys])

# (filter f xs) is the list of items x in xs for which (f x) is true.
\filter==(\f\xs xs [] \x\xs f x (cons x) (); filter f xs)

# (unique xs) is the ordered list of unique elements in list xs.
\unique==
	(\xs
	xs [] \x\xs
	append (unique; filter (gt x) xs); # all the items less than x
	cons x;                            # x itself
	unique; filter (lt x) xs           # all the items greater than x
	)

# (sort xs) is the ordered list of all elements in list xs.
\sort==
	(\xs
	xs [] \x\xs
	append (sort; filter (gt x) xs);   # all the items less than x
	cons x; append (filter (eq x) xs); # all the items equal to x
	sort; filter (lt x) xs             # all the items greater than x
	)

# (tower n f x) is the result of applying f to x n times.
\tower==
	(\n\f\x
	le n 0 x;
	tower (- n 1) f (f x)
	)

# (do xs fn) Apply function fn to each item in list xs.
\do==(\xs\fn xs () \x\xs fn x do xs fn)

\x
eq x "append" {append};
eq x "say" {say};
eq x "sort" {sort};
eq x "tower" {tower};
eq x "do" {do};
eq x "unique" {unique};
void
)

# Here we test the sort.

# A list of 30 random words from diceware.com.
\list =
	[
	"when"
	"scud"
	"gibby"
	"babel"
	"skip"
	"sham"
	"rug"
	"expel"
	"grout"
	"perky"
	"jo"
	"piece"
	"ro"
	"haw"
	"revel"
	"qqqq"
	"bow"
	"blotch"
	"flam"
	"than"
	"subtly"
	"jess"
	"angie"
	"dewar"
	"polka"
	"ur"
	"eb"
	"am"
	"amra"
	"crown"
	]

# The number of times to double the list for the unique sort.
\N=
	(
	# NOTE: If you time these you'll see that the sort time is O(N).
	#7  # 3840 words
	#9  # 15360 words
	#10 # 30720 words
	11 # 61440 words
	#12 # 122880 words
	#13 # 245760 words
	#14 # 491520 words

	# To use 15 you need to raise the limits:
	# ulimit -Ss 160000
	# ulimit -Sv 800000
	# ulimit -St 30
	#15 # 983040 words
	)

# (exp_list n xs) Make 2^n copies of list xs.
\exp_list=(\n\xs tower n (\x append x x) xs)

# Show all the entries of a list on separate lines.
\show=(\xs do xs say)
# Wrap the show inside matching brackets.
\show=(\xs say "[" show xs say "]")

# Make two copies of the list and sort it.
show (sort; exp_list 1 list)

# Make 2^N copies of the list and show only the unique entries.
show (unique; exp_list N list)
