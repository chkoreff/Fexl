# (check x d) = x if x is good, otherwise d.
\check=(\x is_defined x x)
# (default d x) = x if x is good, otherwise d.
\default=(\\d\x is_defined x x d)

\path_under=(\dir\name . (. dir "/") name)

# Establish paths based on command arguments.
\dir_base=(dirname; dirname; argv 0)
\script_name=(default ""; argv 1)

\trace=(fsay stderr)
\error=(\msg trace msg die)

# Define key to refer to a context.
\defc= # LATER 20240820 deprecated
	(\key\cx
	@\\self
	def key self;
	cx
	)

# Define some extra functions.
\std=
(

# (if x y) = y if x is true, otherwise I.
\if=(\\x\\y x y I)

\dir_local=(dirname script_name)

\NL="
"
\TAB="	"
\CR=(chr 13)
\LF=NL
\QU=~ "~

\run_benchmark=
	(\\show\\f
	fexl_benchmark f \val\steps\bytes
	show ["steps "steps" bytes "bytes]
	val
	)

\do_benchmark=(run_benchmark trace)
\show_benchmark=(run_benchmark say)

# Use a file in the directory which contains the user's script.
\use=(\name use_file; path_under dir_local name)
\load=(\name\cx extend cx; use name)

def "defc" defc;
def "if" if;
def "use" use;
def "load" load;

def "check" check;
def "default" default;

def "path_under" path_under;
def "dir_base" dir_base;
def "dir_local" dir_local;

def "trace" trace;
def "error" error;

def "TAB" TAB;
def "NL" NL;
def "CR" CR;
def "LF" LF;
def "QU" QU;

def "do_benchmark" do_benchmark;
def "show_benchmark" show_benchmark;
std
)

# Load libraries into the standard context on demand.
\dir_lib=(path_under dir_base "src/lib")
\use=(\name use_file; path_under dir_lib name)
\load=(\name\cx extend cx; use name)

defc "std";
def "use_lib" use; # LATER 20241018 deprecate
load "read_ssv.fxl";
load "read_csv.fxl";
load "read.fxl";
load "run.fxl";
load "date.fxl";
load "time.fxl";
load "html.fxl";
load "hex.fxl";
load "assoc.fxl";
load "indent.fxl";
load "format.fxl";
load "list.fxl";
load "math.fxl";
load "bool.fxl";
std
