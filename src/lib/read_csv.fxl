# Parse the CSV (comma-separated value) format.
# NOTE: https://www.ietf.org/rfc/rfc4180.txt
# "Spaces are considered part of a field and should not be ignored."
\get_plain_item=
	(\sep
	collect_token
		(\x
		eq x sep T;
		is_eol x T;
		F
		)
	)

# Get a quoted item.  A single QU char is treated as end of string.  Two QU
# chars in a row are treated as a single QU character which appears in the
# string.
\get_quote_item==
	(
	\buf=buf_new
	@\loop
	\x=look
	is_undef x void;
	skip
	eq x QU
		(
		\x=look
		is_undef x (buf_get buf);
		eq x QU
			(
			skip
			buf_put buf QU
			loop
			);
		buf_get buf
		);
	buf_put buf x
	loop
	)

\get_item=
	(\sep
	\x=look
	is_undef x void;
	eq x QU
		(
		skip
		get_quote_item
		)
		(get_plain_item sep)
	)

\get_row=
	(\sep @\loop
	\item=(get_item sep)
	is_undef item void;
	\row=
		(
		\x=look
		is_undef x [];
		skip
		eq x sep
			(
			\row=loop
			is_undef row [] row
			)
			[]
		)
	[item;row]
	)

\get_rows=
	(\sep @\loop
	skip_match is_eol
	\row=(get_row sep)
	is_undef row [];
	\rows=loop
	[row;rows]
	)

\parse=(\read\sep\x read x (get_rows sep))

# Use arbitrary separator.
\read_xsv_string=(parse read_string)
\read_xsv_chars=(parse read_chars)
\read_xsv_file=(parse read_file)

# comma-separated
\read_csv_string=(read_xsv_string ",")
\read_csv_chars=(read_xsv_chars ",")
\read_csv_file=(read_xsv_file ",")

# tab-separated
\read_tsv_string=(read_xsv_string TAB)
\read_tsv_chars=(read_xsv_chars TAB)
\read_tsv_file=(read_xsv_file TAB)

\form
def "read_xsv_string" read_xsv_string;
def "read_xsv_chars" read_xsv_chars;
def "read_xsv_file" read_xsv_file;
def "read_csv_string" read_csv_string;
def "read_csv_chars" read_csv_chars;
def "read_csv_file" read_csv_file;
def "read_tsv_string" read_tsv_string;
def "read_tsv_chars" read_tsv_chars;
def "read_tsv_file" read_tsv_file;
form
