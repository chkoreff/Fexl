# Parse the CSV (comma-separated value) format.
# NOTE: https://www.ietf.org/rfc/rfc4180.txt
# "Spaces are considered part of a field and should not be ignored."
evaluate
(
use_lib "read.fxl";
standard
) \;

\read=
(\;

\get_plain_item==
	(
	collect_to
		(\ch
		is_void ch T;
		eq ch "," T;
		is_eol ch
		)
	)

# Get a char inside a quoted string.  A single QU char is treated as end of
# string.  Two QU chars in a row are treated as a single QU character which
# appears in the string.
\get_quote_char==
	(
	\ch=get
	ne ch QU ch;
	\ch=look
	eq ch QU (skip QU);
	""
	)

# Get a quoted item.
\get_quote_item==
	(
	\buf=buf_new
	@\loop
	\ch=get_quote_char
	eq ch "" (buf_get buf);
	buf_put buf ch
	loop
	)

\get_item==
	(
	\ch=look
	eq ch QU (skip get_quote_item);
	\item=get_plain_item
	# Convert to number if possible.
	\n=(str_num item)
	is_good n n item
	)

# Get a row.
\get_row==
	(@\loop
	\x=get_item
	\ch=get
	ne ch "," [x];
	\xs=loop
	[x;xs]
	)

\get_rows==
	(
	get_seq
		get_row
		is_eol
		is_void
	)

get_rows
)

\read_csv_string=(read_string read)
\read_csv_chars=(read_chars read)
\read_csv_file=(read_file read)

define "read_csv_string" read_csv_string;
define "read_csv_chars" read_csv_chars;
define "read_csv_file" read_csv_file;
standard
