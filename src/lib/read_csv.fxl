# Parse the CSV (comma-separated value) format.
# NOTE: https://www.ietf.org/rfc/rfc4180.txt
# "Spaces are considered part of a field and should not be ignored."

\get_plain_item=
	(\sep
	collect_token
		(\x
		eq x sep T;
		is_eol x T;
		F
		)
	)

# Get a char inside a quoted string.  A single QU char is treated as end of
# string.  Two QU chars in a row are treated as a single QU character which
# appears in the string.
\get_quote_char=
	(\fail\done\keep
	scan fail \x
	eq x QU
		(
		scan done \x
		eq x QU
			(keep QU)
			(push x done)
		)
		(keep x)
	)

# Get a quoted item.
\get_quote_item=
	(\no\yes
	\buf=buf_new
	@\loop
	get_quote_char no
		(\str=(buf_get buf) yes str)
		(\x buf_put buf x loop)
	)

\get_item=
	(\sep
	\get_plain_item=(get_plain_item sep)
	\no\yes
	scan no \x
	eq x QU (get_quote_item no yes);
	push x;
	get_plain_item yes
	)

\get_row=
	(\sep
	\get_item=(get_item sep)
	@\loop\no\yes
	get_item no \item
	scan (yes [item]) \x
	eq x sep
		(loop (yes [item]) \row yes [item;row])
		(yes [item])
	)

\get_rows=
	(\sep
	\get_row=(get_row sep)
	@\loop\yes
	skip_match is_eol;
	get_row (yes []) \row
	loop \rows yes [row;rows]
	)

\read_rows=
	(\sep\read
	\get_rows=(get_rows sep)
	read get_rows
	)

# comma-separated
\read_csv_string=(read_rows ","  read_string)
\read_csv_chars=(read_rows "," read_chars)
\read_csv_file=(read_rows "," read_file)

# tab-separated
\read_tsv_string=(read_rows TAB read_string)
\read_tsv_chars=(read_rows TAB read_chars)
\read_tsv_file=(read_rows TAB read_file)

\form
def "read_csv_string" read_csv_string;
def "read_csv_chars" read_csv_chars;
def "read_csv_file" read_csv_file;
def "read_tsv_string" read_tsv_string;
def "read_tsv_chars" read_tsv_chars;
def "read_tsv_file" read_tsv_file;
form
