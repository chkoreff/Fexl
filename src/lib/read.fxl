#
\scan=(\no\yes\xs xs (no []) yes)
\push=(\x\yes\xs yes [x;xs])

\is_eol=(\x eq x LF T; eq x CR)
\is_white=(ge " ")

# Skip matching characters.
\skip_match=
	(\is_match @\loop\yes
	scan yes \x
	is_match x
		(loop yes)
		(push x yes)
	)

# Collect characters up to an ending condition.
\collect_to=
	(\is_end\no\yes
	\buf=buf_new
	@\loop
	scan no \x
	is_end x
		(\str=(buf_get buf) yes str)
		(buf_put buf x loop)
	)

# Map a get function to a stream of characters.
\stream=(\get @\loop \x=get is_undef x [] [x;loop])

\read_chars=(\read read (\data\xt data))

\read_string=
	(\read\str
	\fh=(readstr str)
	read_chars read (stream; sgetc fh)
	)

\read_file=
	(\read\name
	\fh=(fopen name "r")
	is_void fh void;
	\data=(read_chars read (stream; fgetc fh))
	fclose fh
	data
	)

\form
def "scan" scan;
def "push" push;
def "is_eol" is_eol;
def "is_white" is_white;
def "skip_match" skip_match;
def "collect_to" collect_to;
def "read_chars" read_chars;
def "read_string" read_string;
def "read_file" read_file;
form
