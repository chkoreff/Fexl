# (Sat 2018-07-28 In memoriam, Charlie the cat, thank you little friend.)

\get_key=
	(\key @\\loop \list
	list void \pair\list
	pair \this_key\\this_val
	eq key this_key this_val;
	loop list
	)

\del_key=
	(\key @\\loop \list
	list [] \pair\list
	pair \this_key\\this_val
	eq key this_key list;
	\list=(loop list)
	[pair;list]
	)

\put_key=
	(\key\val
	@\\loop \list
	list [{key val}] \pair\list
	pair \this_key\\this_val
	eq key this_key [{this_key val};list];
	\list=(loop list)
	[pair;list]
	)

\push_key=
	(\key\val\list
	[{key val};list]
	)

\update_key=
	(\key\update @\\loop \list
	list
		(
		\val=(update void)
		[{key val}]
		)
	\pair\list
	pair \this_key\\this_val
	eq key this_key
		(
		\val=(update this_val)
		[{this_key val};list]
		);
	\list=(loop list)
	[pair;list]
	)

\add_key=
	(\key\val
	update_key key (\total is_defined total (+ total val) val)
	)

\update_values=
	(\update
	map
		(\pair
		pair \key\\val
		\val=(update key val)
		{key val}
		)
	)

# (sort_pairs list) Sort the list of pairs by key.
\sort_pairs=(sort_all (\p p \k\_ k))

\put_default=
	(\key\dval\list
	\val=(get_key key list)
	is_defined val list;
	[{key dval};list]
	)

\name_columns=
	(@\\loop\keys\vals
	keys [] \key\keys
	vals [] \val\vals
	\tail=(loop keys vals)
	[{key val};tail]
	)

\make_assoc=
	(\extract\xs
	\xs=(map (\x \k=(extract x) {k x}) xs)
	combine_keys (\x\_ x) xs # Keep the first.
	)

define ":" get_key; # Handy synonym
define "get_key" get_key;
define "del_key" del_key;
define "put_key" put_key;
define "push_key" push_key;
define "update_key" update_key;
define "add_key" add_key;
define "update_values" update_values;
define "sort_pairs" sort_pairs;
define "put_default" put_default;
define "name_columns" name_columns;
define "make_assoc" make_assoc;
void
