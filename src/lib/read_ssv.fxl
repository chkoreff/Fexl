# Parse the SSV (space-separated value) format.
\\get_plain_item=
	(
	collect_to
		(
		at_white T;
		at_ch QU T;
		at_ch "~" T;
		at_eof T;
		F
		)
	)

\\get_quote_item=
	(
	skip
	\buf=buf_new
	collect_to_ch buf QU (buf_get buf) void
	)

\\get_tilde_item=
	(
	\buf=buf_new
	eq 1 (collect_tilde_string buf) (buf_get buf) void
	)

\\get_item=
	(
	at_eof void;
	at_ch QU get_quote_item;
	at_ch "~" get_tilde_item;
	at_eol (skip void);
	get_plain_item
	)

\\get_row=
	(@\\loop
	skip_match (at_eol F; at_white)
	\item=get_item
	is_undef item void;
	\row=loop
	is_undef row [item] [item;row]
	)

\\get_rows=
	(@\\loop
	skip_white
	\row=get_row
	is_undef row [];
	\rows=loop
	[row;rows]
	)

\parse=(\read\x read x get_rows)

\read_ssv_string=(parse read_stream)
\read_ssv_chars=(parse read_chars)
\read_ssv_file=(parse read_file)

define "read_ssv_string" read_ssv_string;
define "read_ssv_chars" read_ssv_chars;
define "read_ssv_file" read_ssv_file;
void
