#
evaluate
(
:: standard;
:: (load "bool.fxl");
:: (load "math.fxl");
:: (load "list.fxl");
void
) \;

# Repeat string x n times
\repeat_str=
	(\x
	\buf=buf_new
	@\loop \n
	\n=n
	le n 0 (buf_get buf);
	buf_put buf x
	loop (- n 1)
	)

\format_num=
(
# Split string x into groups of three with the given separator.
\split3=
	(\sep @\loop \x
	\x=x
	\len=(length x)
	le len 3 x
	\pos=(- len 3)
	\prefix=(slice x 0 pos)
	\suffix=(slice x pos 3)
	. (loop prefix) (. sep suffix)
	)

# Format number x to n decimal places, with an optional separator for the
# integer part.
\format_num=
	(\n\sep\x
	# Get the list of all digits without the decimal point.
	\n=n
	\x=x
	\factor=(^ 10 n)
	\digits=(num_str; round; * factor (abs x))

	# Pad 0s on the front as needed.
	\pad=(repeat_str "0" (+ 1 (- n (length digits))))
	\digits=(. pad digits)

	# Extract integer and fractional part.
	\pos=(- (length digits) n)
	\xi=(slice digits 0 pos)
	\xf=(slice digits pos n)

	# Insert separator if desired.
	\xi=(eq sep "" xi (split3 sep xi))

	# Put a "." in front of any fractional part.
	\xf=(eq xf "" "" (. "." xf))
	# Determine the sign of result.
	\sign=(lt x 0 "-" "")

	\result=(. sign; . xi xf)
	result
	)
format_num
)

\format_money=(format_num 2 ",")

# Quote a string, enclosing it within terminators to make it suitable for
# reading as a Fexl string.
\fexl_quote=
	(\x
	\x=x
	\term=
		(
		\check=(\y is_void (search x y 0) y)
		check QU;
		(@\loop\count
		\term=(. "~"; num_str count)
		check term;
		\count=(+ count 1)
		loop count
		) 1
		)
	. term; . (eq term QU "" " "); . x; term
	)

# Convert string to upper-case.  Only works with ASCII characters though.
# LATER 20160331 Make it work with full utf8.
\uc=
	(
	str_map \ch
	\n=(ord ch)
	and (ge n 97) (le n 122) (chr; - n 32) ;
	ch
	)

# Convert string to lower-case.
\lc=
	(
	str_map \ch
	\n=(ord ch)
	and (ge n 65) (le n 90) (chr; + n 32) ;
	ch
	)

\nearly_zero=(\x lt (abs x) 1e-8)

\render_data=
	(\put
	@\put_data\x
	\x=x
	is_str x (put (fexl_quote x));
	is_num x (put (nearly_zero x "0" (num_str x)));
	is_tuple x
		(
		put "{"

		(@\loop\space\xs
		split_tuple xs () \xs\x
		loop T xs
		put_data x
		if space (put " ")
		) F x

		put "}"
		);
	is_list x
		(
		put "["

		(@\loop\space\xs
		xs () \x\xs
		if space (put " ")
		put_data x
		loop T xs
		) F x

		put "]"
		);
	is_bool x (put (x "T" "F"));
	is_void x (put "void");
	put "()"
	)

\render_list=
	(\put\xs
	\xs=xs

	\nl==(put NL)
	\say=(\x put x nl)
	\put_data=(render_data put)

	not (is_list xs) (put_data xs nl);

	say "["
	each xs (\x put_data x nl)
	say "]"
	)

\fput_data=(\fh \put=(fput fh) render_data put)
\fput_list=(\fh \put=(fput fh) render_list put)

\put_data=(fput_data stdout)
\put_list=(fput_list stdout)

\show_data=(\label\x put [label" "] put_data x nl)

\data_to_str=
	(\data
	\buf=buf_new
	\put=(buf_put buf)
	render_data put data
	buf_get buf
	)

\str_to_data=
	(\str
	evaluate
		(
		def "T" T;
		def "F" F;
		def "void" void;
		void
		);
	parse str ""
	)

# (count_match at_beg match str) Count the number of matching characters at
# the start or end of the string.
\count_match=
	(\at_beg\match\str
	\max=(- (length str) 1)
	\index=(at_beg () (- max))
	(@\loop\N
	gt N max N;
	\ch=(slice str (index N) 1)
	match ch (\N=(+ N 1) loop N) N
	) 0
	)

# Trim matching characters at the start and/or end of the string.
\str_trim=
	(\match\at_beg\at_end\str
	\str=str
	\beg=(at_beg (count_match T match str) 0)
	\end=(at_end (count_match F match str) 0)
	\len=(max 0; - (length str) (+ beg end))
	slice str beg len
	)

\str_trim_spaces=(str_trim (\ch le ch " "))

# Trim spaces at the start and end of the string.  A space is defined as any
# ASCII character less than or equal to the space character, which includes all
# control characters.
\trim_spaces=(str_trim_spaces T T)

# Trim spaces at the start of the string.
\trim_beg_spaces=(str_trim_spaces T F)

# Trim spaces at the end of the string.
\trim_end_spaces=(str_trim_spaces F T)

def "repeat_str" repeat_str;
def "format_num" format_num;
def "format_money" format_money;
def "fexl_quote" fexl_quote;
def "uc" uc;
def "lc" lc;
def "render_data" render_data;
def "render_list" render_list;
def "fput_data" fput_data;
def "fput_list" fput_list;
def "put_data" put_data;
def "put_list" put_list;
def "show_data" show_data;
def "as_str" data_to_str; # handy synonym
def "data_to_str" data_to_str;
def "str_to_data" str_to_data;
def "count_match" count_match;
def "str_trim" str_trim;
def "trim_spaces" trim_spaces;
def "trim_beg_spaces" trim_beg_spaces;
def "trim_end_spaces" trim_end_spaces;
void
