# Quote a character for embedding in HTML.
\quote_ch=
	(\ch
	eq ch "&" "&amp;" ;
	eq ch ~ "~ "&quot;" ;
	eq ch "<" "&lt;" ;
	eq ch ">" "&gt;" ;
	ch
	)

# Quote a string for embedding in HTML.
\quote_str=(str_map quote_ch)

# Quote arbitrary data.
\quote=
	(
	@\quote\x \x=x
	is_str x (quote_str x);
	is_num x x;
	is_tuple x
		(
		(@\quote_tuple\xs
		split_tuple xs {} \xs\x
		\xs=(quote_tuple xs)
		\x=(quote x)
		join_tuple xs x
		) x
		);
	is_list x (map quote x);
	is_bool x x;
	void
	)

\colgroup=
	(\widths
	say "<colgroup>"
	each widths (\width say ["<col width=" width ">"])
	say "</colgroup>"
	)

\tag_enclosure=
	(\desc
	\beg=(to_str desc)
	\pos=(search beg " " 0)
	\end=(is_good pos (slice beg 0 pos) beg)
	{ beg end }
	)

\_tag=
	(\desc\body
	tag_enclosure desc \beg\end
	put ["<" beg ">"]
	body
	put ["</" end ">"]
	)

\tag=
	(\desc\body
	tag_enclosure desc \beg\end
	put ["<" beg ">"]
	(
	eq end "td" I;
	eq end "th" I;
	eq end "a" I;
	nl
	)
	body
	put ["</" end ">"]
	nl
	)

\tr=(tag "tr")

\put_quote_data=
	(
	\put=(\x put (quote x))
	render_data put
	)

\put_quote_list=
	(
	\put=(\x put (quote x))
	render_list put
	)

# https://en.wikipedia.org/wiki/Percent-encoding
# LATER 20171108 accelerate this, perhaps in C.
\encode_percent=
	(str_map \ch
	\is_reserved=
		(
		eq ch "!" T;
		eq ch "*" T;
		eq ch "'" T;
		eq ch "(" T;
		eq ch ")" T;
		eq ch ";" T;
		eq ch ":" T;
		eq ch "@" T;
		eq ch "&" T;
		eq ch "=" T;
		eq ch "+" T;
		eq ch "$" T;
		eq ch "," T;
		eq ch "/" T;
		eq ch "?" T;
		eq ch "#" T;
		eq ch "[" T;
		eq ch "]" T;
		eq ch "%" T;
		F
		)

	is_reserved
		(. "%" (hex_byte (ord ch)))
		ch
	)

# LATER 20170802 Note that QU isn't considered reserved.  Test that on a real
# web site.
\make_url=
	(\params
	\params=
		(map_good
			(\pair pair \key\val
			\val=(is_num val (num_str val) val)
			eq val "" void;
			\q_key=(encode_percent key)
			\q_val=(encode_percent val)
			[q_key"="q_val]
			)
			params)

	\url=(join "&amp;" params)
	\url=(eq url "" "." (. "?" url))
	url
	)

define "quote_ch" quote_ch;
define "quote" quote;
define "colgroup" colgroup;
define "_tag" _tag;
define "tag" tag;
define "tr" tr;
define "put_quote_data" put_quote_data;
define "put_quote_list" put_quote_list;
define "encode_percent" encode_percent;
define "make_url" make_url;
standard
