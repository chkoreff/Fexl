# Run the user's script given by argv[1] in the enhanced standard context.

\TAB="	"
\NL="
"
\CR=(chr 13)
\LF=NL
\QU=~ "~

# (if x y) = y if x is true, other I.
\if=(\x\y x y I)

# (check x d) = x if x is good, otherwise d.
\check=(\x \x=x is_defined x x)
# (default d x) = x if x is good, otherwise d.
\default=(\d\x \x=x is_defined x x d)

\trace=(fsay stderr)
\error=(\msg trace msg die)

\path_under=(\dir\name . (. dir "/") name)

# Return an object with key defined as val.
\with=
	(\key\val\obj
	\obj=obj
	\x
	eq x key val;
	obj x
	)

# Return a context with key defined as val.
\def=(\key\val with key {val})

# Use a file given by a direct path.
\use_file=(\name\context evaluate context; parse_file name)

# Establish paths based on command arguments.
\dir_base=(dirname; dirname; argv 0)
\dir_lib=(path_under dir_base "src/lib")

\script_name=(default "" (argv 1))
\dir_local=(dirname script_name)

# Use a file in the Fexl library directory.
\use_lib=(\name use_file (path_under dir_lib name))

# Use a file in the directory which contains the user's script.
\use=(\name use_file (path_under dir_local name))

# The :: function is used to chain contexts together.
\::=
	(\load\prev
	\prev=prev
	\next=(load prev)

	\key
	\val=(next key)
	is_defined val val;
	prev key
	)

# The ?:: function is like ::, except it loads only on demand.
\?::=
	(\load\prev
	\prev=prev
	\next==(\= load prev)

	\key
	\val=(prev key)
	is_defined val val;
	next key
	)

# Create a cache that evaluates a function and stores the value at a given key.
# Subsequent calls with the same key return the originally stored value.
\new_cache==
	(
	\table=var_new
	\key\fn

	\val=(var_get table key)
	is_defined val val;

	\val=fn
	var_put table (with key val; var_get table)
	val
	)

# Evaluate the user's script.
use_file script_name;

?:: (use_lib "run.fxl");
?:: (use_lib "format.fxl");
?:: (use_lib "hex.fxl");
?:: (use_lib "list.fxl");
?:: (use_lib "math.fxl");
?:: (use_lib "bool.fxl");

def "def" def;
def "if" if;
def "with" with;
def "::" ::;
def "?::" ?::;

def "TAB" TAB;
def "NL" NL;
def "CR" CR;
def "LF" LF;
def "QU" QU;

def "check" check;
def "default" default;
def "use_file" use_file;
def "use_lib" use_lib;
def "use" use;

def "path_under" path_under;
def "dir_base" dir_base;
def "dir_local" dir_local;

def "trace" trace;
def "error" error;

def "new_cache" new_cache;
standard
