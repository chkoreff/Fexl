# Run the user's script given by argv[1] in the enhanced standard context.

# (check x d) = x if x is good, otherwise d.
\check=(\x is_defined x x)
# (default d x) = x if x is good, otherwise d.
\default=(\\d\x is_defined x x d)

\path_under=(\dir\name . (. dir "/") name)

# Establish paths based on command arguments.
\dir_base=(dirname; dirname; argv 0)
\script_name=(default ""; argv 1)

\trace=(fsay stderr)
\error=(\msg trace msg die)

# Define key to refer to a context.
\defc=
	(\key\cx
	@\\self
	def key self;
	cx
	)

# Define some extra functions.
\\extra=
(\=

# (if x y) = y if x is true, other I.
\if=(\\x\\y x y I)

\dir_local=(dirname script_name)

\NL="
"
\TAB="	"
\CR=(chr 13)
\LF=NL
\QU=~ "~

\run_benchmark=
	(\\show\\f
	fexl_benchmark f \val\steps\bytes
	show ["steps "steps" bytes "bytes]
	val
	)

\do_benchmark=(run_benchmark trace)
\show_benchmark=(run_benchmark say)

# Use a file in the directory which contains the user's script.
\use=(\name use_file; path_under dir_local name)

def "defc" defc;
def "if" if;
def "use" use;

def "check" check;
def "default" default;

def "path_under" path_under;
def "dir_base" dir_base;
def "dir_local" dir_local;

def "trace" trace;
def "error" error;

def "TAB" TAB;
def "NL" NL;
def "CR" CR;
def "LF" LF;
def "QU" QU;

def "do_benchmark" do_benchmark;
def "show_benchmark" show_benchmark;
void
)

# Enhance the standard context to load libraries on demand.
\std=
(
\dir_lib=(path_under dir_base "src/lib")
\use=(\name use_file; path_under dir_lib name)

\\bool=
	(\=
	value std;
	use "bool.fxl"
	)

\\math=
	(\=
	value std;
	use "math.fxl"
	)

\\list=
	(\=
	value (:: bool; std);
	use "list.fxl"
	)

\\format=
	(\=
	value (:: list; :: math; :: bool; :: extra; std);
	use "format.fxl"
	)

\\indent=
	(\=
	value (:: format; :: extra; std);
	use "indent.fxl"
	)

\\assoc=
	(\=
	value (:: list; std);
	use "assoc.fxl"
	)

\\hex=
	(\=
	value (:: list; :: math; :: bool; std);
	use "hex.fxl"
	)

\\html=
	(\=
	value (:: hex; :: format; :: list; :: extra; std);
	use "html.fxl"
	)

\\time=
	(\=
	value (:: extra; std);
	use "time.fxl"
	)

\\date=
	(\=
	value (:: list; :: math; std);
	use "date.fxl"
	)

\\run=
	(\=
	value (:: list; :: bool; :: extra; std);
	use "run.fxl"
	)

\\read=
	(\=
	value std;
	use "read.fxl"
	)

\\read_csv=
	(\=
	value (:: read; :: extra; std);
	use "read_csv.fxl"
	)

\\read_ssv=
	(\=
	value (:: read; :: extra; std);
	use "read_ssv.fxl"
	)

\load=
	(\module

	\path=(path_under dir_base (. "lib/"; . module ".so"))
	\lib=(dlopen path)
	is_void lib (error dlerror);

	\sym=(. "type_cx_" module)
	\entry=(dlsym lib sym)
	is_void entry (error ["Could not find symbol "sym]);

	entry ()
	)

defc "std";
:: std;
def "load" load;
def "use_lib" use;
:: extra;
:: bool;
:: math;
:: list;
:: format;
:: indent;
:: assoc;
:: hex;
:: html;
:: time;
:: date;
:: run;
:: read_csv;
:: read_ssv;
void
)

# Evaluate the user's script.
value std; use_file script_name
